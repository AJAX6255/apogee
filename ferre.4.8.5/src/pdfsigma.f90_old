subroutine	pdfsigma(chiscale,w,pf,obs,lambda_obs,mobs,lsfarr,e,sp,lchi,cov)

!
!	Calculating the covariance matrix by sampling the likelyhood function
!	IMPORTANT: The stellar parameters are updated too
!

use share, only: dp,ndim,nov,indv,npix,nlambda1,lambda_syn,winter, &
                                    mlsf,nlsf


implicit none

!in/out
real(dp), intent(in)	   :: chiscale		!chi**2/sum(w_i(f_i-obs_i))^2)
real(dp), intent(in)       :: w(nlambda1)       ! weights
real(dp), intent(inout)    :: pf(ndim)      ! vector of fixed parameters
real(dp), intent(in)       :: obs(nlambda1)     ! vector of observations
real(dp), intent(in)       :: lambda_obs(nlambda1)  ! wavelengths for observations
real(dp), intent(in)       :: mobs             ! mean or median of obs array
real(dp), intent(in)       :: lsfarr(mlsf,nlsf)    ! lsfarray
real(dp), intent(in)  	   :: e(nlambda1)		!errors in the observations
real(dp), intent(out)	   :: sp(ndim)		!vector of uncertainties
real(dp), intent(out)  	   :: lchi		!log10(chi**2/(nlambda1-nov+1))
real(dp), intent(out)      :: cov(nov,nov)      !covariance matrix

!locals
integer,parameter  :: nsigma = 4				!how far we go to eval likelyhood
integer			   :: nsampling	= 100 			!n. of points for sampling
integer		  	   :: i,j						!counters
real(dp)		   :: flux(nlambda1)			!interpolated flux vector
real(dp)		   :: low(ndim),high(ndim)		!low/hi vals for each norm. parameter
real(dp)		   :: temp						!tmp variable
real(dp)		   :: ptemp(ndim)				!tmp array	
real(dp)		   :: num(nov),den				!tmp variables


!evaluate lchi
call flx(pf,lambda_obs,mobs,lsfarr,flux)
lchi=0.0_dp
lchi=sum(w*(obs(1:nlambda1)-flux(1:nlambda1))**2)
lchi=log10(lchi*chiscale/(nlambda1-nov+1))	

sp(1:ndim)=0.0_dp		!initialize


if (lchi >= 4.0d0) then 
!don't even try with lost causes. Very poorly matched spectra 
!are simply assigned sp(*)=-1.00

  	do j=1,nov
		sp(indv(j))=-1.00_dp
  	enddo

else
	!calculate covariance matrix, just like in covsigma
	call cova(w,lambda_obs,mobs,lsfarr,pf,e,cov)
	
	!use diagonal elements to get std. deviation
  	do j=1,nov
		if (cov(j,j) >=0.0) then
			sp(indv(j))=sqrt(cov(j,j))
		else
			sp(indv(j))=-1._dp
		endif
  	enddo
  	
  	!now we know where to start, let's find the 4-sigma 
  	!intervals in each parameter
  	do j=1,ndim
  		low(j)=pf(j)-nsigma*sp(j)
  		high(j)=pf(j)+nsigma*sp(j)
  		if (low(j)   < 0.0) low(j)=0.0_dp
  		if (high(j) >= 1.0) high(j)=0.9999999_dp
  		write(*,*)'j,low,high=',j,low(j),high(j)
  	enddo

	!generate random sampling data points
	write(*,*)'antes  pf=',pf
	num(1:nov)=0.0_dp
	den=0.0_dp
	do j=1,nsampling
	   	ptemp(1:ndim)=pf
	   	do i=1,nov
		     	call random_number(temp)
		     	ptemp(indv(i))=temp*(high(i)-low(i))+low(i)
	   	enddo
	   	call flx(ptemp,lambda_obs,mobs,lsfarr,flux)
	   	lchi=0.0_dp
		lchi=sum(w*(obs(1:nlambda1)-flux(1:nlambda1))**2)
		lchi=lchi*chiscale/(nlambda1-nov+1)  !chi**2
		den=den+exp(-lchi/2._dp)
		do i=1,nov
		 	num(i)=num(i)+ptemp(indv(i))*exp(-lchi/2._dp)
		enddo
	enddo
	write(*,*)'den=',den
	pf(1:nov)=0.0_dp
	pf(1:nov)=num(1:nov)/den
	write(*,*)'despues  ptemp=',pf

endif

end subroutine	pdfsigma
